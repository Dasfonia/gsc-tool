/* Copyright 2020 xensik. All rights reserved.
//
// Use of this source code is governed by a GNU GPLv3 license
// that can be found in the LICENSE file.
*/
%option outfile="./IW6/compiler_lexer.cpp" header-file="./IW6/compiler_lexer.hpp"
%option reentrant
%option noyywrap batch nounput noinput
%option never-interactive
%option nounistd

%{
#include "IW6.hpp"
#include "compiler_parser.hpp"
%}

RGX_FILEPATH        ([_A-Za-z0-9]+\\)+[_A-Za-z0-9]+
RGX_IDENTIFIER      [_A-Za-z][_A-Za-z0-9\-]*
RGX_STRING		    \"(?:\\.|[^\"])*?\"|\'(?:\\.|[^\'])*?\'
RGX_FLOAT		    -?[0-9]+\.(?:[0-9]*)?f?|\.[0-9]+f?
RGX_INTEGER		    -?[0-9]+
RGX_DEFAULT		    (.|\n)

%x COMMENT_BLOCK_STATE
%x DEVELOPER_BLOCK_STATE

%%

[ \t\n\r]

"//".*

"/*"					    { BEGIN(COMMENT_BLOCK_STATE); }
<COMMENT_BLOCK_STATE>(.|\n)
<COMMENT_BLOCK_STATE>"*/"	{ BEGIN(INITIAL); }

<INITIAL>"/#"				{ BEGIN(DEVELOPER_BLOCK_STATE); }
<DEVELOPER_BLOCK_STATE>"#/"	{ BEGIN(INITIAL); }	

"#include"			{ return yy::parser::make_INCLUDE(); }
"#using_animtree"	{ return yy::parser::make_USING_ANIMTREE(); }
"#animtree"			{ return yy::parser::make_ANIMTREE(); }
"if"			    { return yy::parser::make_IF(); }
"else"			    { return yy::parser::make_ELSE(); }
"while"			    { return yy::parser::make_WHILE(); }
"for"			    { return yy::parser::make_FOR(); }
"foreach"			{ return yy::parser::make_FOREACH(); }
"in"		        { return yy::parser::make_IN(); }
"switch"		    { return yy::parser::make_SWITCH(); }
"case"			    { return yy::parser::make_CASE(); }
"default"		    { return yy::parser::make_DEFAULT(); }
"break"			    { return yy::parser::make_BREAK(); }
"continue"		    { return yy::parser::make_CONTINUE(); }
"return"		    { return yy::parser::make_RETURN(); }
"wait"			    { return yy::parser::make_WAIT(); }
"waittill"			{ return yy::parser::make_WAITTILL(); }
"waittillmatch"		{ return yy::parser::make_WAITTILLMATCH(); }
"waittillframeend"	{ return yy::parser::make_WAITTILLFRAMEEND(); }
"endon"			    { return yy::parser::make_ENDON(); }
"notify"		    { return yy::parser::make_NOTIFY(); }
"thread"		    { return yy::parser::make_THREAD(); }
"undefined"		    { return yy::parser::make_UNDEFINED(); }
"true"		        { return yy::parser::make_TRUE(); }
"false"		        { return yy::parser::make_FALSE(); }
"size"		        { return yy::parser::make_SIZE(); }
"game"		        { return yy::parser::make_GAME(); }
"level"		        { return yy::parser::make_LEVEL(); }

"[]"                { return yy::parser::make_EMPTY_ARRAY(); }
\(			        { return yy::parser::make_LPAREN(); }
\)			        { return yy::parser::make_RPAREN(); }
\{			        { return yy::parser::make_LBRACE(); }
\}			        { return yy::parser::make_RBRACE(); }
\[			        { return yy::parser::make_LBRACKET(); }
\]			        { return yy::parser::make_RBRACKET(); }
\,			        { return yy::parser::make_COMMA(); }
\.			        { return yy::parser::make_DOT(); }
\:\:		        { return yy::parser::make_DOUBLECOLON(); }
\:			        { return yy::parser::make_COLON(); }
\;			        { return yy::parser::make_SEMICOLON(); }
\?			        { return yy::parser::make_QMARK(); }

\+\+		        { return yy::parser::make_INC(); }
\-\-		        { return yy::parser::make_DEC(); }
\<\<\=		        { return yy::parser::make_ASSIGN_LSHIFT(); }
\>\>\=		        { return yy::parser::make_ASSIGN_RSHIFT(); }
\<\<		        { return yy::parser::make_LSHIFT(); }
\>\>		        { return yy::parser::make_RSHIFT(); }
\|\|		        { return yy::parser::make_CMP_OR(); }
\&\&		        { return yy::parser::make_CMP_AND(); }
\=\=		        { return yy::parser::make_CMP_EQUAL(); }
\!\=		        { return yy::parser::make_CMP_NOT_EQ(); }
\<\=		        { return yy::parser::make_CMP_LESS_EQ(); }
\>\=		        { return yy::parser::make_CMP_GREATER_EQ(); }
\<			        { return yy::parser::make_CMP_LESS(); }
\>			        { return yy::parser::make_CMP_GREATER(); }
\+\=		        { return yy::parser::make_ASSIGN_ADD(); }
\-\=		        { return yy::parser::make_ASSIGN_SUB(); }
\*\=		        { return yy::parser::make_ASSIGN_MULT(); }
\/\=			    { return yy::parser::make_ASSIGN_DIV(); }
\%\=			    { return yy::parser::make_ASSIGN_MOD(); }
\|\=		        { return yy::parser::make_ASSIGN_BW_OR(); }
\&\=		        { return yy::parser::make_ASSIGN_BW_AND(); }
\^\=		        { return yy::parser::make_ASSIGN_BW_XOR(); }
\=			        { return yy::parser::make_ASSIGN(); }
\+			        { return yy::parser::make_ADD(); }
\-			        { return yy::parser::make_SUB(); }
\*			        { return yy::parser::make_MULT(); }
\/			        { return yy::parser::make_DIV(); }
\%			        { return yy::parser::make_MOD(); }
\!			        { return yy::parser::make_NOT(); }
\~			        { return yy::parser::make_COMPLEMENT(); }
\|			        { return yy::parser::make_BW_OR(); }
\&			        { return yy::parser::make_BW_AND(); }
\^			        { return yy::parser::make_BW_XOR(); }

{RGX_FILEPATH}		{ return yy::parser::make_FILEPATH(std::string(yytext)); }
{RGX_IDENTIFIER}	{ return yy::parser::make_IDENTIFIER(std::string(yytext)); }
\#{RGX_STRING}	    { return yy::parser::make_STRING_HASH(std::string(yytext)); }
\&{RGX_STRING}	    { return yy::parser::make_STRING_LOC(std::string(yytext)); }
{RGX_STRING}	    { return yy::parser::make_STRING(std::string(yytext)); }
{RGX_FLOAT}		    { return yy::parser::make_FLOAT(std::string(yytext)); }
{RGX_INTEGER}	    { return yy::parser::make_INTEGER(std::string(yytext)); }

<<EOF>>             { return yy::parser::make_YYEOF(); }
<*>{RGX_DEFAULT}    { throw yy::parser::syntax_error("invalid character: " + std::string(yytext)); }
%%
